# Grammar for the Zerg Language

## All kinds of the valid statement
stmt          : ( simple_stmt | compound_stmt | inline_asm ) NEWLINE
simple_stmt   : 'nop' | 'continue' | 'break' | expression | assign | print_stmt | inc_dec
compound_stmt : if_stmt | while_stmt | for_stmt | func_stmt | cls_stmt

if_stmt       : 'if'    test ':'  scope ( 'elif' ':' scope )* [ 'else' ':' scope ]
while_stmt    : 'while' test ':'  scope
for_stmt      : 'for' varargs 'in' expression  ':' scope
func_stmt     : 'func'  VAR  '('  [ parameter ] ')' ':' scope
cls_stmt      : 'cls'   VAR  '('  [ varargs ]      ')' ':' scope
print_stmt    : 'print' expression
inc_dec       : VAR ( '++' | '--' )
inline_asm    : 'asm' STRING
scope         : NEWLINE INDENT stmt+ DEDENT

assign		: VAR '=' expression | expression '->' VAR
varargs     : VAR ( ',' VAR )*
kwargs      : VAR '=' test | kwargs ',' VAR '=' test
parameter   : ( varargs | VAR '=' expression ) [ varargs ]
argument    : ( expression | kwargs ) [ ',' kwargs ]

expression  : [ 'not' ] test | expression ',' test
## test expression, return true or false
test        : xor_test    | or_test    'or'  xor_test
xor_test    : and_test    | xor_test   'xor' and_test
and_test    : equal_test  | and_test   'and' equal_test
equal_test  : comp_test   | equal_test ('eq' | 'not' 'eq' ) comp_test
comp_test   : bitwise_or  | comp_test  ( '<' | '>' ) bitwise_or

## expression
bitwise_or  : bitwise_xor | bitwise_or  '|' bitwise_xor
bitwise_xor : bitwise_and | bitwise_xor '^' bitwise_and
bitwise_and : shift_expr  | bitwise_and '&' shift_expr
shift_expr  : arith_expr  | shift_expr  ( '<<' | '>>' ) arith_expr
arith_expr  : factor_expr | arith_expr  ( '+' | '-' ) factor_expr
factor_expr : pow_expr    | factor_expr ( '*' | '/' | '%' | '~' ) pow_expr
pow_expr    : term        | term '**' pow_expr

## Should be the minimal element
term        : atom | ( '+' | '-' | '~' | 'not' ) term
atom        : VAR  | NUMBER | pair_atom | atom '.' atom | funccall | getter
funccall    : atom '(' expression ')'
getter      : atom '[' expression ']'
pair_atom	: '(' expression ')' | '[' expression ']' | '{' expression '}'
